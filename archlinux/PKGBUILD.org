# SPDX-License-Identifier: GPL-3.0-or-later
# SPDX-FileCopyrightText: Copyright © 2021 Erez Geva <ErezGeva2@gmail.com>
#
# Maintainer: Erez Geva <ErezGeva2@gmail.com>
#
# @author Erez Geva <ErezGeva2@@gmail.com>
# @copyright © 2021 Erez Geva
#
# Maintainer: Erez Geva <ErezGeva2@gmail.com>
#
# See:
# https://wiki.archlinux.org/title/PKGBUILD
# https://man.archlinux.org/man/PKGBUILD.5.en
###############################################################################
pkgver=1.4
pkgrel=1
pkgname=(libptpmgmt libptpmgmt-openssl libptpmgmt-gcrypt libptpmgmt-gnutls
  libptpmgmt-nettle libptpmgmt-dev libptpmgmt-doc
  libptpmgmt-perl lua-ptpmgmt ruby-ptpmgmt tcl-ptpmgmt go-ptpmgmt
  python3-ptpmgmt pmc-ptpmgmt phc-ctl-ptpmgmt)
# Source code can be build on any architecture
# But as Arch Linux does not support the 'all' keyword
arch=(x86_64)
url="https://$pkgname.nwtime.org"
license=('LGPL-3.0-or-late')
#source=("$pkgname-$pkgver.tar.gz::https://github.com/erezgeva/$pkgname/archive/refs/tags/$pkgver.tar.gz")
source=("$pkgname-$pkgver.txz")
# Target library directory
libdir='usr/lib'
# version for depends
verdep="$pkgver-$pkgrel"
build() {
  cd $pkgname-$pkgver
  autoreconf -i
  ./configure --prefix=/usr --sbindir=\${prefix}/bin
  make PMC_USE_LIB=so --no-print-directory all doxygen
  # Place here as make do not use packages!
  make DESTDIR=../install install --no-print-directory compile_py3
}
makedepends=(m4 doxygen graphviz perl ruby tcl swig lua lua51 lua52 lua53
  python3 go gcc libtool autoconf make chrpath
  texlive-fontutils which pandoc openssl libgcrypt gnutls nettle)
# You may use 'patchelf' as alternative of `chrpath`
package_libptpmgmt() {
  pkgdesc='PTP management library, to communicate with ptp4l'
  mkdir -p $pkgdir/$libdir
  mv $srcdir/install/usr/lib/$pkgbase.so.* $pkgdir/$libdir
}
package_libptpmgmt-openssl() {
  pkgdesc='PTP management library HMAC plugin using the openssl library'
  depends=("$pkgbase=$verdep" openssl)
  mkdir -p $pkgdir/$libdir
  mv $srcdir/install/usr/lib/${pkgname/-/_}.so.* $pkgdir/$libdir
}
package_libptpmgmt-gcrypt() {
  pkgdesc='PTP management library HMAC plugin using the gcrypt library'
  depends=("$pkgbase=$verdep" libgcrypt)
  mkdir -p $pkgdir/$libdir
  mv $srcdir/install/usr/lib/${pkgname/-/_}.so.* $pkgdir/$libdir
}
package_libptpmgmt-gnutls() {
  pkgdesc='PTP management library HMAC plugin using the gnutls library'
  depends=("$pkgbase=$verdep" gnutls)
  mkdir -p $pkgdir/$libdir
  mv $srcdir/install/usr/lib/${pkgname/-/_}.so.* $pkgdir/$libdir
}
package_libptpmgmt-nettle() {
  pkgdesc='PTP management library HMAC plugin using the nettle library'
  depends=("$pkgbase=$verdep" nettle)
  mkdir -p $pkgdir/$libdir
  mv $srcdir/install/usr/lib/${pkgname/-/_}.so.* $pkgdir/$libdir
}
package_libptpmgmt-dev() {
  # development package can use static library
  # But as Arch linux does not use development packages, we add it here
  options=(staticlibs)
  pkgdesc='PTP management library development, provides header files and static library.'
  depends=("$pkgbase=$verdep")
  mkdir -p $pkgdir/$libdir $pkgdir/usr/share/doc
  mv $srcdir/install/usr/share/doc/$pkgname $pkgdir/usr/share/doc
  mv $srcdir/install/usr/include $pkgdir/usr
  mv $srcdir/install/usr/lib/$pkgbase*.a $pkgdir/$libdir
  mv $srcdir/install/usr/lib/$pkgbase*.so $pkgdir/$libdir
  mv $srcdir/install/usr/lib/pkgconfig $pkgdir/$libdir
}
package_libptpmgmt-doc() {
  arch=(any)
  license=('GFDL-1.3-no-invariants-or-later')
  pkgdesc='PTP management library documentation, to communicate with ptp4l'
  mkdir -p $pkgdir/usr/share/doc
  mv $srcdir/install/usr/share/doc/$pkgname $pkgdir/usr/share/doc
}
package_libptpmgmt-perl() {
  pkgdesc='PTP management library Perl wrapper'
  depends=("$pkgbase=$verdep" perl)
  mkdir -p $pkgdir/$libdir
  cp -a $srcdir/install/usr/lib/perl* $pkgdir/$libdir
  rm -rf $srcdir/install/usr/lib/perl*/*/core_perl/auto/PtpMgmtLib
  rm -f $srcdir/install/usr/lib/perl*/*/core_perl/PtpMgmtLib*
}
package_python3-ptpmgmt() {
  pkgdesc='PTP management library python version 3 wrapper'
  depends=("$pkgbase=$verdep" python3)
  mkdir -p $pkgdir/$libdir
  cp -a $srcdir/install/usr/lib/python3* $pkgdir/$libdir
}
package_lua-ptpmgmt() {
  pkgdesc='PTP management library Lua wrapper'
  depends=("$pkgbase=$verdep")
  optdepends=(lua lua51 lua52 lua53)
  mkdir -p $pkgdir/$libdir
  mv $srcdir/install/usr/lib/liblua*ptpmgmt* $pkgdir/$libdir
  cp -a $srcdir/install/usr/lib/lua* $pkgdir/$libdir
  rm -f $srcdir/install/usr/lib/lua*/*/ptpmgmt*
}
package_ruby-ptpmgmt() {
  pkgdesc='PTP management library ruby wrapper'
  depends=("$pkgbase=$verdep" ruby)
  mkdir -p $pkgdir/$libdir
  cp -a $srcdir/install/usr/lib/ruby $pkgdir/$libdir
  rm -f $srcdir/install/usr/lib/ruby/vendor_ruby/*/*/*ptpmgmt*
}
package_tcl-ptpmgmt() {
  pkgdesc='PTP management library tcl wrapper'
  depends=("$pkgbase=$verdep" tcl)
  mkdir -p $pkgdir/$libdir
  mv $srcdir/install/usr/lib/ptpmgmt $pkgdir/$libdir
}
package_go-ptpmgmt() {
  arch=(any)
  pkgdesc='PTP management library golang development wrapper'
  depends=("$pkgbase=$verdep" go)
  mkdir -p $pkgdir/$libdir
  mv $srcdir/install/usr/lib/go $pkgdir/$libdir
}
#package_php-ptpmgmt() {
# Arch linux php do not have system include_path!
# pkgdesc='PTP management library php wrapper'
# depends=("$pkgbase=$verdep" php)
# mkdir -p $pkgdir/$libdir
# mv $srcdir/install/usr/lib/php* $pkgdir/$libdir
#}
package_pmc-ptpmgmt() {
  license=('GPL-3.0-or-later')
  pkgdesc="pmc tool. new rewrite of linuxptp pmc tool using the $pkgbase library."
  depends=("$pkgbase=$verdep")
  mkdir -p $pkgdir/usr/local/man/man8 $pkgdir/usr/bin
  mv $srcdir/install/usr/bin/pmc* $pkgdir/usr/bin
  mv $srcdir/install/usr/share/man/man8/pmc* $pkgdir/usr/local/man/man8
}
package_phc-ctl-ptpmgmt() {
  arch=(any)
  license=('GPL-3.0-or-later')
  pkgdesc="phc_ctl tool. new rewrite of linuxptp phc_ctl tool using the $pkgbase library."
  depends=("python3-ptpmgmt=$verdep")
  mkdir -p $pkgdir/usr/local/man/man8 $pkgdir/usr/bin
  mv $srcdir/install/usr/bin/phc_ctl* $pkgdir/usr/bin
  mv $srcdir/install/usr/share/man/man8/phc_ctl* $pkgdir/usr/local/man/man8
}
changelog=changelog
# vim: set ft=bash:
